# NixOS configuration for the ISO build VM in Proxmox (VMID 9998).
#
# This is a minimal system with the tools needed to build bloud ISOs:
# Nix with flakes, Go, Node.js, git, rsync.
#
# One-time setup:
#   1. Create a NixOS VM in Proxmox: VMID=9998, 40GB disk, 8GB RAM, 4 cores
#   2. Boot from NixOS minimal ISO, install NixOS, and add to /etc/nixos/configuration.nix:
#        services.openssh.enable = true;
#        users.users.root.initialPassword = "bloud";
#        nix.settings.experimental-features = ["nix-command" "flakes"];
#      Then: nixos-install --no-root-passwd && reboot
#   3. Run: ./bloud setup-builder
#
# Subsequent ISO builds: ./bloud start --build

{ lib, pkgs, ... }:

{
  imports = [
    # Generated by nixos-generate-config during initial install
    /etc/nixos/hardware-configuration.nix
  ];

  services.qemuGuest.enable = true;

  boot.loader.grub = {
    enable = lib.mkDefault true;
    device = lib.mkDefault "/dev/vda";
  };

  networking = {
    hostName = "bloud-builder";
    useDHCP = true;
    firewall.allowedTCPPorts = [ 22 ];
  };

  nix = {
    settings = {
      experimental-features = [ "nix-command" "flakes" ];
      trusted-users = [ "root" "bloud" ];
      max-jobs = "auto";
    };
    gc = {
      automatic = true;
      dates = "weekly";
      options = "--delete-older-than 14d";
    };
  };

  environment.systemPackages = with pkgs; [
    go
    nodejs_22
    git
    rsync
    jq
  ];

  # Required so nix flake can evaluate source trees synced via rsync
  environment.etc."gitconfig".text = ''
    [safe]
      directory = *
  '';

  users.users.bloud = {
    isNormalUser = true;
    extraGroups = [ "wheel" ];
    initialPassword = "bloud";
  };

  security.sudo.extraRules = [{
    users = [ "bloud" ];
    commands = [{ command = "ALL"; options = [ "NOPASSWD" ]; }];
  }];

  services.openssh = {
    enable = true;
    settings = {
      PasswordAuthentication = true;
      PermitRootLogin = "yes"; # Required for ./bloud setup-builder
    };
  };

  system.stateVersion = "24.11";
}
