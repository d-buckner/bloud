{ config, pkgs, lib, ... }:

let
  appCfg = config.bloud.apps.traefik;
  bloudCfg = config.bloud;
  mkPodmanService = import ../../nixos/lib/podman-service.nix { inherit pkgs lib; };

  userHome = "/home/${bloudCfg.user}";
  configPath = "${userHome}/.local/share/${bloudCfg.dataDir}";
in
{
  options.bloud.apps.traefik = {
    enable = lib.mkEnableOption "Traefik reverse proxy";

    port = lib.mkOption {
      type = lib.types.int;
      default = 8080;
      description = "Port for Traefik web entrypoint (path-based routing)";
    };

    apiPort = lib.mkOption {
      type = lib.types.int;
      default = 3000;
      description = "Port for the host-agent API backend";
    };

    uiPort = lib.mkOption {
      type = lib.types.int;
      default = 5173;
      description = "Port for the Bloud UI (Vite dev server)";
    };
  };

  config = lib.mkIf appCfg.enable {
    # Create Traefik configuration files
    system.activationScripts.bloud-traefik-config = lib.stringAfter [ "users" ] ''
      mkdir -p ${configPath}/traefik/dynamic

      # Main Traefik config
      cat > ${configPath}/traefik/traefik.yml <<'EOF'
entryPoints:
  web:
    address: ":${toString appCfg.port}"

providers:
  file:
    directory: /etc/traefik/dynamic/
    watch: true

api:
  dashboard: true

log:
  level: DEBUG
EOF

      # Base routes (static - UI, API, dashboard, common middlewares)
      # App-specific routes are generated dynamically in apps-routes.yml by host-agent
      cat > ${configPath}/traefik/dynamic/base.yml <<'EOF'
http:
  routers:
    # Traefik dashboard (access via /dashboard/)
    traefik-dashboard:
      rule: "PathPrefix(`/dashboard`)"
      service: api@internal
      priority: 95

    # Host agent API
    host-api:
      rule: "PathPrefix(`/api`)"
      service: host-agent
      priority: 90

    # Bloud UI (catch-all, handles /{appname} routes internally)
    bloud-ui:
      rule: "PathPrefix(`/`)"
      middlewares:
        - cross-origin-isolation
      service: bloud-ui
      priority: 1

  middlewares:
    # Cross-origin isolation headers for SharedArrayBuffer support
    # 'credentialless' enables SharedArrayBuffer while being more permissive than 'require-corp'
    cross-origin-isolation:
      headers:
        customResponseHeaders:
          Cross-Origin-Opener-Policy: "same-origin"
          Cross-Origin-Embedder-Policy: "credentialless"

    # Common middleware for iframe embedding - removes restrictive headers from apps
    iframe-headers:
      headers:
        customResponseHeaders:
          X-Frame-Options: ""
          Content-Security-Policy: ""

    # Embed isolation headers - matches parent page COOP/COEP
    embed-isolation:
      headers:
        customResponseHeaders:
          Cross-Origin-Opener-Policy: "same-origin"
          Cross-Origin-Embedder-Policy: "credentialless"

  services:
    host-agent:
      loadBalancer:
        servers:
          - url: "http://localhost:${toString appCfg.apiPort}"

    bloud-ui:
      loadBalancer:
        servers:
          - url: "http://localhost:${toString appCfg.uiPort}"
EOF

      # App routes (generated by host-agent traefikgen)
      # Create empty placeholder if not exists
      if [ ! -f ${configPath}/traefik/dynamic/apps-routes.yml ]; then
        cat > ${configPath}/traefik/dynamic/apps-routes.yml <<'EOF'
# Generated by Bloud - DO NOT EDIT MANUALLY
# This file is managed by the Bloud host agent
# Traefik watches this file for changes

# No routable apps installed yet
EOF
      fi

      chown -R ${bloudCfg.user}:users ${configPath}
    '';

    # Traefik container service
    systemd.user.services.podman-traefik = mkPodmanService {
      name = "traefik";
      image = "traefik:v3.0";
      ports = [
        "${toString appCfg.port}:${toString appCfg.port}"
      ];
      volumes = [
        "${configPath}/traefik/traefik.yml:/etc/traefik/traefik.yml:ro"
        "${configPath}/traefik/dynamic:/etc/traefik/dynamic:ro"
      ];
      network = "host";
    };
  };
}
